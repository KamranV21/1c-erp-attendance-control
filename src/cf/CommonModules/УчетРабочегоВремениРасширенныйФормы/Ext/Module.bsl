
#Область СлужебныеПроцедурыИФункции 

&После("ТабельДанныеВРеквизит")
Процедура КРВС_ТабельДанныеВРеквизит(Форма)
	
	КРВС_КонтрольРабочегоВремениСотрудниковКлиентСервер.ЗаполнитьИтогиНеобоснованныхОтсутствийПоСотрудникам(Форма.Объект);
	
КонецПроцедуры

&После("ТабельЗаполнитьДанныеПоСотрудникам")
Процедура КРВС_ТабельЗаполнитьДанныеПоСотрудникам(Форма, СписокСотрудников, СтрокиПоСотрудникам, ВысотаСтроки, ЗаполнятьТерриторииИУсловия)
	
	Сотрудники = ?(СписокСотрудников = Неопределено, КРВС_СотрудникиПоДокументу(Форма.Объект), СписокСотрудников);
	
	КРВС_КонтрольРабочегоВремениСотрудников.ЗаполнитьТаблицуКонтроляПосещаемостиНормативнымиЧасами(Форма.Объект, Сотрудники);
	КРВС_ЗаполнитьЯвкиПоДаннымПосещаемостиНаСервере(Форма, Сотрудники);
	КРВС_РассчитатьНеобоснованныеЧасыОтсутствияСотрудников(Форма, Сотрудники);
	
КонецПроцедуры

Процедура КРВС_ЗаполнитьЯвкиПоДаннымПосещаемостиНаСервере(Форма, Сотрудники)
	
	Объект = Форма.Объект; 
		
	ДанныеПосещаемости = КРВС_КонтрольРабочегоВремениСотрудников.ДанныеПосещаемостиЗаПериод(Объект.Организация, Сотрудники, Объект.ДатаНачалаПериода, Объект.ДатаОкончанияПериода);         
	ВыборкаДанныхПосещения = ДанныеПосещаемости.Выбрать();
	
	Явка = Справочники.ВидыИспользованияРабочегоВремени.Явка;
	
	Год = Год(Объект.ПериодРегистрации);
	Месяц = Месяц(Объект.ПериодРегистрации);
	
	Для Каждого СтрокаДанныхВремени Из Объект.ДанныеОВремени Цикл
		
		Для День = 1 По 31 Цикл
			
			Если СтрокаДанныхВремени["ВидВремени" + День] <> Явка Тогда   
				Продолжить;
			КонецЕсли;
			
			Дата = Дата(Год, Месяц, День);
			
			СтруктураПоиска = Новый Структура;
			СтруктураПоиска.Вставить("Сотрудник", СтрокаДанныхВремени.Сотрудник); 
			СтруктураПоиска.Вставить("Дата", Дата);
			
			ЧасыПосещений = 0;
			ВыборкаДанныхПосещения.Сбросить();
			Если ВыборкаДанныхПосещения.НайтиСледующий(СтруктураПоиска) Тогда
				Если ВыборкаДанныхПосещения.ОсвобожденОтКонтроля Тогда
					Продолжить;
				КонецЕсли;
				ЧасыПосещений = ВыборкаДанныхПосещения.Часы;		
			КонецЕсли; 
			
			ЧасыПоТабелю = СтрокаДанныхВремени["Часов" + День];
			
			СтрокаДанныхВремени["Часов" + День] = Мин(ЧасыПоТабелю, ЧасыПосещений);    
						
		КонецЦикла;
		
	КонецЦикла;   
	
	ТабельПриЧтенииНаСервере(Форма);

КонецПроцедуры

Процедура КРВС_РассчитатьНеобоснованныеЧасыОтсутствияСотрудников(Форма, Сотрудники)
	
	Объект = Форма.Объект; 
		
	Для Каждого Сотрудник Из Сотрудники Цикл
		
		СтруктураПоискаДанныхВремени = Новый Структура;
		СтруктураПоискаДанныхВремени.Вставить("Сотрудник", Сотрудник);
		СтрокиВремениПоСотруднику = Объект.ДанныеОВремени.НайтиСтроки(СтруктураПоискаДанныхВремени);
						
		Для День = 1 По 31 Цикл
			КРВС_КонтрольРабочегоВремениСотрудниковКлиентСервер.РассчитатьНеобоснованныеЧасыОтсутствияСотрудника(Объект, Сотрудник, День, СтрокиВремениПоСотруднику);
		КонецЦикла;
		
	КонецЦикла;
	
	КРВС_КонтрольРабочегоВремениСотрудниковКлиентСервер.ЗаполнитьИтогиНеобоснованныхОтсутствийПоСотрудникам(Объект);
	
КонецПроцедуры

Функция КРВС_СотрудникиПоДокументу(Объект)
		
	ТаблицаСотрудников = Объект.ДанныеОВремени.Выгрузить(, "Сотрудник"); 
	ТаблицаСотрудников.Свернуть("Сотрудник");
	
	Возврат ТаблицаСотрудников.ВыгрузитьКолонку("Сотрудник");

КонецФункции

#КонецОбласти
