
#Область ПрограммныйИнтерфейс

// Возвращает итоговую таблицу необоснованных часов сотрудников.
//
// Параметры:
//  ДокументТабеля - ДокументОбъект.ТабельУчетаРабочегоВремени - Табель учета рабочего времени.
//  Сотрудники - Массив Из СправочникСсылка.Сотрудники - Сотрудники к отбору.
// 
// Возвращаемое значение:
//  Массив Из Структура - свернутая таблица итогов необоснованных часов по сотрудникам. 
//
Функция ИтогиНеобоснованныхОтсутствийПоСотрудникам(Знач ДокументТабеля, Сотрудники = Неопределено) Экспорт
	
	КонтрольПосещаемости = ДокументТабеля.КРВС_КонтрольПосещаемости.Выгрузить(, "Сотрудник, НормаЧасов, ОбоснованныеЧасы, НеобоснованныеЧасы");
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	КонтрольПосещаемости.Сотрудник КАК Сотрудник,
	                      |	КонтрольПосещаемости.НормаЧасов КАК НормаЧасов,
	                      |	КонтрольПосещаемости.ОбоснованныеЧасы КАК ОбоснованныеЧасы,
	                      |	КонтрольПосещаемости.НеобоснованныеЧасы КАК НеобоснованныеЧасы
	                      |ПОМЕСТИТЬ ВТ_КонтрольПосещаемости
	                      |ИЗ
	                      |	&КонтрольПосещаемости КАК КонтрольПосещаемости
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ВТ_КонтрольПосещаемости.Сотрудник КАК Сотрудник,
	                      |	СУММА(ВТ_КонтрольПосещаемости.НормаЧасов) КАК НормаЧасов,
	                      |	СУММА(ВТ_КонтрольПосещаемости.ОбоснованныеЧасы) КАК ОбоснованныеЧасы,
	                      |	СУММА(ВТ_КонтрольПосещаемости.НеобоснованныеЧасы) КАК НеобоснованныеЧасы
	                      |ИЗ
	                      |	ВТ_КонтрольПосещаемости КАК ВТ_КонтрольПосещаемости
	                      |ГДЕ
	                      |	ВЫБОР
	                      |			КОГДА &ЕстьОтборПоСотрудникам
	                      |				ТОГДА ВТ_КонтрольПосещаемости.Сотрудник В (&Сотрудники)
	                      |			ИНАЧЕ ИСТИНА
	                      |		КОНЕЦ
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	ВТ_КонтрольПосещаемости.Сотрудник");
	Запрос.УстановитьПараметр("КонтрольПосещаемости", КонтрольПосещаемости);
	Запрос.УстановитьПараметр("Сотрудники", Сотрудники);
	Запрос.УстановитьПараметр("ЕстьОтборПоСотрудникам", Сотрудники <> Неопределено);
	
	Возврат ОбщегоНазначения.ТаблицаЗначенийВМассив(Запрос.Выполнить().Выгрузить());	
	
КонецФункции

#КонецОбласти
