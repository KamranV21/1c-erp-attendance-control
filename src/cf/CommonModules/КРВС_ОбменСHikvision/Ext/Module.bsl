
#Область ПрограммныйИнтерфейс

// Возвращает данные посещаемости сотрудников за период
//
// Параметры:
//  ПараметрыЗагрузки - Структура:
//   * ДатаНачала - Дата  
//   * ДатаНачала - ДатаОкончания
// 
// Возвращаемое значение:
//  ТаблицаЗначений - таблица посещаемости сотрудников.
//
Функция ДанныеПосещаемостиЗаПериод(ПараметрыЗагрузки) Экспорт
	
    РезультатЗапроса = РезультатЗапросаДанныхПосещаемостиЗаПериод(ПараметрыЗагрузки);
	
	ТаблицаПосещаемости = РезультатЗапросаПосещаемостиВТаблицуЗначений(РезультатЗапроса);

	ТаблицаПосещаемостиДополненная = ДополнитьТаблицуПосещаемостиСсылочнымиДанными(ТаблицаПосещаемости);
	
	Возврат ТаблицаПосещаемостиДополненная;

КонецФункции

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

Функция РезультатЗапросаДанныхПосещаемостиЗаПериод(ПараметрыЗагрузки)
			
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	dbo_AttendanceTable.EmployeeID КАК EmployeeID,
	|	dbo_AttendanceTable.AccessDate КАК AccessDate,
	|	dbo_AttendanceTable.AccessTime КАК AccessTime
	|ИЗ
	|	ВнешнийИсточникДанных.КРВС_Hikvision.Таблица.dbo_AttendanceTable КАК dbo_AttendanceTable
	|ГДЕ
	|	dbo_AttendanceTable.AccessDate В (&ДатыОтбора)"); 

	ДатыОтбора = ДатыОтбораДанныхПосещаемостиЗаПериод(ПараметрыЗагрузки.ДатаНачала, ПараметрыЗагрузки.ДатаОкончания);	
	Запрос.УстановитьПараметр("ДатыОтбора", ДатыОтбора);

	Возврат Запрос.Выполнить();	
	
КонецФункции

Функция ДатыОтбораДанныхПосещаемостиЗаПериод(Знач ДатаНачала, Знач ДатаОкончания)
	
	СекундВСутках = 86400;
	
	ДатыОтбора = Новый Массив;	
	
	Пока ДатаНачала <= ДатаОкончания Цикл
		ДатаСтрокой = Формат(ДатаНачала, "ДФ=yyyy-MM-dd");
		ДатыОтбора.Добавить(ДатаСтрокой); 
		ДатаНачала = ДатаНачала + СекундВСутках;
	КонецЦикла;
	
	Возврат ДатыОтбора;

КонецФункции 

Функция РезультатЗапросаПосещаемостиВТаблицуЗначений(РезультатЗапроса)
		
	ТаблицаПосещаемости = НоваяТаблицаПосещаемостиСотрудниковПоДаннымHikvision();
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		СтрокаТаблицыПосещаемости = ТаблицаПосещаемости.Добавить();
		СтрокаТаблицыПосещаемости.ИдентификаторHikvision = Выборка.EmployeeID;
		СтрокаТаблицыПосещаемости.Дата = СтроковыеФункцииКлиентСервер.СтрокаВДату(Выборка.AccessDate, ЧастиДаты.Дата);
		СтрокаТаблицыПосещаемости.Время = СтроковыеФункцииКлиентСервер.СтрокаВДату(Выборка.AccessTime, ЧастиДаты.Время);
	КонецЦикла; 
	
	Возврат ТаблицаПосещаемости;

КонецФункции

Функция НоваяТаблицаПосещаемостиСотрудниковПоДаннымHikvision()
	
	КвалификаторСтроки = Новый КвалификаторыСтроки(50);
	ОписаниеТипаСтрока = Новый ОписаниеТипов("Строка", , , , КвалификаторСтроки);
	
	КвалификаторДаты = Новый КвалификаторыДаты(ЧастиДаты.Дата);
	ОписаниеТипаДата = Новый ОписаниеТипов("Дата", , , , КвалификаторДаты);

	КвалификаторВремени = Новый КвалификаторыДаты(ЧастиДаты.Время);
	ОписаниеТипаВремя = Новый ОписаниеТипов("Дата", , , , КвалификаторВремени);
	
	ТаблицаПосещаемости = Новый ТаблицаЗначений;
	ТаблицаПосещаемости.Колонки.Добавить("ИдентификаторHikvision", ОписаниеТипаСтрока);
	ТаблицаПосещаемости.Колонки.Добавить("Дата", ОписаниеТипаДата);
	ТаблицаПосещаемости.Колонки.Добавить("Время", ОписаниеТипаВремя);

	Возврат ТаблицаПосещаемости;		

КонецФункции

Функция ДополнитьТаблицуПосещаемостиСсылочнымиДанными(ТаблицаПосещаемости)

	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	ТаблицаПосещаемости.ИдентификаторHikvision КАК ИдентификаторHikvision,
	                      |	ТаблицаПосещаемости.Дата КАК Дата,
	                      |	ТаблицаПосещаемости.Время КАК Время
	                      |ПОМЕСТИТЬ ВТ_Посещаемость
	                      |ИЗ
	                      |	&ТаблицаПосещаемости КАК ТаблицаПосещаемости
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ВТ_Посещаемость.ИдентификаторHikvision КАК ИдентификаторHikvision,
	                      |	МАКСИМУМ(Сотрудники.Ссылка) КАК Сотрудник
	                      |ПОМЕСТИТЬ ВТ_Сотрудники
	                      |ИЗ
	                      |	ВТ_Посещаемость КАК ВТ_Посещаемость
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
	                      |		ПО ВТ_Посещаемость.ИдентификаторHikvision = Сотрудники.КРВС_ИдентификаторHikvision
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	ВТ_Посещаемость.ИдентификаторHikvision
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ВТ_Сотрудники.Сотрудник КАК Сотрудник,
	                      |	ВТ_Посещаемость.Дата КАК Дата,
	                      |	ВТ_Посещаемость.Время КАК Время
	                      |ИЗ
	                      |	ВТ_Посещаемость КАК ВТ_Посещаемость
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Сотрудники КАК ВТ_Сотрудники
	                      |		ПО ВТ_Посещаемость.ИдентификаторHikvision = ВТ_Сотрудники.ИдентификаторHikvision
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	Дата,
	                      |	Время");
	Запрос.УстановитьПараметр("ТаблицаПосещаемости", ТаблицаПосещаемости);   
	
	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции

#КонецОбласти