	
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура КРВС_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	КРВС_ОпределитьДопустимоеВремяОтсутствийБезОбоснований();
	
	КРВС_УстановитьУсловноеОформление();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапки

&НаКлиенте
Процедура КРВС_ОрганизацияПриИзмененииПосле(Элемент)
	
	КРВС_ОпределитьДопустимоеВремяОтсутствийБезОбоснований();		
	
КонецПроцедуры

&НаКлиенте
Процедура КРВС_ПериодВводаДанныхОВремениПриИзмененииПосле(Элемент)
	
	КРВС_ОпределитьДопустимоеВремяОтсутствийБезОбоснований();		
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДанныеОВремени

&НаКлиенте
Процедура КРВС_ДанныеОВремениПриАктивизацииЯчейкиПосле(Элемент)
	
	ТекущиеДанные = Элементы.ДанныеОВремени.ТекущиеДанные; 
	Если ТекущиеДанные = Неопределено Тогда
		КРВС_ОбнулитьДанныеПосещаемостиСотрудникаЗаДень();
		Возврат;
	КонецЕсли;
	
	НомерДня = УчетРабочегоВремениРасширенныйКлиентСервер.ТабельНомерДняПоИмениЭлемента(Элемент.ТекущийЭлемент.Имя);
	
	Если НомерДня = Неопределено Тогда 
		КРВС_ОбнулитьДанныеПосещаемостиСотрудникаЗаДень();
		Возврат;
	КонецЕсли;  
	
	КРВС_ВывестиДатуАктивнойЯчейкиТабеля(НомерДня);
	КРВС_ВывестиДанныеПосещаемостиСотрудникаЗаДень(ТекущиеДанные.Сотрудник, НомерДня);

КонецПроцедуры

&НаКлиенте
Процедура КРВС_ДанныеОВремениВремяПредставлениеПриИзмененииПосле(Элемент)
	
	НомерДня = УчетРабочегоВремениРасширенныйКлиентСервер.ТабельНомерДняПоИмениЭлемента(Элемент.Имя);
	ДанныеТекущейСтроки = Элементы.ДанныеОВремени.ТекущиеДанные;
	КРВС_КонтрольРабочегоВремениСотрудниковКлиентСервер.РассчитатьНеобоснованныеЧасыОтсутствияСотрудника(Объект, ДанныеТекущейСтроки.Сотрудник, НомерДня);			
	
	КРВС_ВывестиДанныеПосещаемостиСотрудникаЗаДень(ДанныеТекущейСтроки.Сотрудник, НомерДня);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
&После("ПериодРегистрацииПриИзменении")
Процедура КРВС_ПериодРегистрацииПриИзменении()
	
	КРВС_ОпределитьДопустимоеВремяОтсутствийБезОбоснований();

КонецПроцедуры

&НаСервере
Процедура КРВС_ОпределитьДопустимоеВремяОтсутствийБезОбоснований()

	КРВС_ДопустимыеЧасыБезОбоснований = КРВС_КонтрольРабочегоВремениСотрудников.ДопустимыеЧасыОтсутствийБезОбоснований(Объект.Организация, Объект.ДатаНачалаПериода, Объект.ДатаОкончанияПериода);	

КонецПроцедуры

&НаСервере
Процедура КРВС_УстановитьУсловноеОформление()
		
	//  

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДанныеОВремениКРВС_НеобоснованныеЧасы.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ДанныеОВремени.ЭтоПерваяСтрокаПоСотруднику");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ДанныеОВремени.КРВС_НеобоснованныеЧасы");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 0;

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветФона", WebЦвета.БледноЗеленый);
	
	//  

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДанныеОВремениКРВС_НеобоснованныеЧасы.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ДанныеОВремени.ЭтоПерваяСтрокаПоСотруднику");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ДанныеОВремени.КРВС_НеобоснованныеЧасы");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Больше;
	ОтборЭлемента.ПравоеЗначение = 0;

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветФона", WebЦвета.СветлоЗолотистый);
	
	//  

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДанныеОВремениКРВС_НеобоснованныеЧасы.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ДанныеОВремени.ЭтоПерваяСтрокаПоСотруднику");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ДанныеОВремени.КРВС_НеобоснованныеЧасы");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Больше;
	ОтборЭлемента.ПравоеЗначение = Новый ПолеКомпоновкиДанных("КРВС_ДопустимыеЧасыБезОбоснований");

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветФона", WebЦвета.ЛососьСветлый);
	
КонецПроцедуры

&НаСервере
Процедура КРВС_ВывестиДанныеПосещаемостиСотрудникаЗаДень(Сотрудник, НомерДня)

	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("Сотрудник", Сотрудник);
	СтруктураПоиска.Вставить("НомерДня", НомерДня);  
	
	СтрокиКонтроляПоСотруднику = Объект.КРВС_КонтрольПосещаемости.НайтиСтроки(СтруктураПоиска); 
	Если СтрокиКонтроляПоСотруднику.Количество() = 0 Тогда 
		КРВС_ОбнулитьДанныеПосещаемостиСотрудникаЗаДень();
		Возврат;
	КонецЕсли; 
	
	СтрокаКонтроляВремениПоСотруднику = СтрокиКонтроляПоСотруднику[0]; 
	
	КРВС_НормаЧасовЗаДень = СтрокаКонтроляВремениПоСотруднику.НормаЧасов;
	КРВС_ОбоснованныеЧасыЗаДень = СтрокаКонтроляВремениПоСотруднику.ОбоснованныеЧасы;
	КРВС_НеобоснованныеЧасыЗаДень = СтрокаКонтроляВремениПоСотруднику.НеобоснованныеЧасы; 
		
КонецПроцедуры

&НаСервере
Процедура КРВС_ВывестиДатуАктивнойЯчейкиТабеля(НомерДня)

	ИндексДня = НомерДня - 1;
	СекундВСутках = 86400;

	КРВС_ТекущийДень = Объект.ПериодРегистрации + ИндексДня * СекундВСутках;
	
КонецПроцедуры

&НаСервере
Процедура КРВС_ОбнулитьДанныеПосещаемостиСотрудникаЗаДень()

	КРВС_НормаЧасовЗаДень = 0;
	КРВС_ОбоснованныеЧасыЗаДень = 0;
	КРВС_НеобоснованныеЧасыЗаДень = 0;

КонецПроцедуры

#КонецОбласти 
