#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий
	
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
    СтандартнаяОбработка = Ложь;
	
    КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
    Настройки = КомпоновщикНастроек.ПолучитьНастройки();
    МакетКомпоновкиДанных = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, Настройки, ДанныеРасшифровки);
	
	СтандартныйПериод = Настройки.ПараметрыДанных.Элементы.Найти("СтандартныйПериод").Значение;    
	Если Месяц(СтандартныйПериод.ДатаНачала) <> Месяц(СтандартныйПериод.ДатаОкончания) Тогда 
		СтандартныйПериод.ДатаОкончания = КонецМесяца(СтандартныйПериод.ДатаНачала);
		Настройки.ПараметрыДанных.Элементы.Найти("СтандартныйПериод").Значение = СтандартныйПериод;
	КонецЕсли;
	МенеджерВременныхТаблиц = МенеджерВременныхТаблицОтчета(СтандартныйПериод);
	
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
    ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных, , ДанныеРасшифровки, Истина,, МенеджерВременныхТаблиц);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
    ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
    ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных, Истина);
	
	ДокументРезультат = КомпоновкаДанныхСервер.ОбработатьЗаголовки(ДокументРезультат);
	
КонецПроцедуры   

#КонецОбласти  

#Область СлужебныеПроцедурыИФункции

Функция МенеджерВременныхТаблицОтчета(СтандартныйПериод)

	ПараметрыПолученияДанных = УчетРабочегоВремениРасширенный.ПараметрыДляЗапросВТДанныеУчетаВремениИСостоянийСотрудников();
	ПараметрыПолученияДанных.ДатаНачала = СтандартныйПериод.ДатаНачала;
	ПараметрыПолученияДанных.ДатаОкончания = СтандартныйПериод.ДатаОкончания;
	ПараметрыПолученияДанных.МесяцДатаНачала = НачалоМесяца(СтандартныйПериод.ДатаНачала);
	ПараметрыПолученияДанных.МесяцДатаОкончания = КонецМесяца(СтандартныйПериод.ДатаНачала);
	ПараметрыПолученияДанных.ДатаАктуальности = ТекущаяДатаСеанса();  
	ПараметрыПолученияДанных.РассчитыватьПлановоеВремя = Истина;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Сотрудники.Ссылка КАК Сотрудник,
	|	НАЧАЛОПЕРИОДА(&ДатаНачала, МЕСЯЦ) КАК Месяц,
	|	&ДатаАктуальности КАК ДатаАктуальности,
	|	&ДатаНачала КАК ДатаНачала,
	|	&ДатаОкончания КАК ДатаОкончания
	|ПОМЕСТИТЬ ВТСотрудники
	|ИЗ
	|	Справочник.Сотрудники КАК Сотрудники");
    
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ДатаНачала", СтандартныйПериод.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", СтандартныйПериод.ДатаОкончания);
	Запрос.УстановитьПараметр("ДатаАктуальности", ТекущаяДатаСеанса());
	Запрос.Выполнить();
	
	УчетРабочегоВремениРасширенный.СоздатьВТДанныеУчетаВремениИСостоянийСотрудников(МенеджерВременныхТаблиц, Истина, ПараметрыПолученияДанных);
	
	Возврат МенеджерВременныхТаблиц; 
	
КонецФункции
	
#КонецОбласти

#КонецЕсли
